;IRQ 单中断应用实验 实验程序清单（T8259-1.ASM）

IRQ10_IVADD EQU 01C8H ;IRQ10 对应的中断矢量地址
IRQ10_OCW1 EQU 0A1H ;IRQ10 对应PC 机内部8259 的OCW1 地址
IRQ10_OCW2 EQU 0A0H ;IRQ10 对应PC 机内部8259 的OCW2 地址
IRQ10_IM EQU 0FBH ;IRQ10 对应的中断屏蔽字

IRQ7_IVADD EQU 003CH ;IRQ7 对应的中断矢量地址
IRQ7_OCW1 EQU 021H ;IRQ7 对应PC 机内部8259 的OCW1 地址
IRQ7_OCW2 EQU 020H ;IRQ7 对应PC 机内部8259 的OCW2 地址
IRQ7_IM EQU 07FH ;IRQ7 对应的中断屏蔽字


STACK1 SEGMENT STACK
DW 256 DUP(?)
STACK1 ENDS

DATA SEGMENT
MES DB 'Press any key to exit!',0AH,0DH,0AH,0DH,'$'
CS10_BAK DW ? ;保存IRQ10 原中断处理程序入口段地址的变量
IP10_BAK DW ? ;保存IRQ10 原中断处理程序入口偏移地址的变量
IM10_BAK DB ? ;保存IRQ10 原中断屏蔽字的变量
CS7_BAK DW ? ;保存IRQ7 原中断处理程序入口段地址的变量
IP7_BAK DW ? ;保存IRQ7 原中断处理程序入口偏移地址的变量
IM7_BAK DB ? ;保存IRQ7 原中断屏蔽字的变量
DATA ENDS



CODE SEGMENT
ASSUME CS:CODE,DS:DATA
START: MOV AX,DATA
MOV DS,AX

MOV DX,OFFSET MES ;显示退出提示
MOV AH,09H
INT 21H

CLI
MOV AX,0000H ;替换IRQ10 的中断矢量
MOV ES,AX
MOV DI,IRQ10_IVADD
MOV AX,ES:[DI]
MOV IP10_BAK,AX ;保存IRQ10 原中断处理程序入口偏移地址
MOV AX,OFFSET MYISR10
MOV ES:[DI],AX ;设置当前中断处理程序入口偏移地址

ADD DI,2
MOV AX,ES:[DI]
MOV CS10_BAK,AX ;保存IRQ10 原中断处理程序入口段地址
MOV AX,SEG MYISR10
MOV ES:[DI],AX ;设置当前中断处理程序入口段地址
MOV DX,IRQ10_OCW1 ;设置中断屏蔽寄存器，打开IRQ10 的屏蔽位
IN AL,DX
MOV IM10_BAK,AL ;保存IRQ10 原中断屏蔽字
AND AL,IRQ10_IM
OUT DX,AL

MOV AX,0000H ;替换IRQ7 的中断矢量
MOV ES,AX
MOV DI,IRQ7_IVADD
MOV AX,ES:[DI]
MOV IP7_BAK,AX ;保存IRQ10 原中断处理程序入口偏移地址
MOV AX,OFFSET MYISR7
MOV ES:[DI],AX ;设置当前中断处理程序入口偏移地址

ADD DI,2
MOV AX,ES:[DI]
MOV CS7_BAK,AX ;保存IRQ7 原中断处理程序入口段地址
MOV AX,SEG MYISR7
MOV ES:[DI],AX ;设置当前中断处理程序入口段地址
MOV DX,IRQ7_OCW1 ;设置中断屏蔽寄存器，打开IRQ10 的屏蔽位
IN AL,DX
MOV IM7_BAK,AL ;保存IRQ7 原中断屏蔽字
AND AL,IRQ7_IM
OUT DX,AL
STI

WAIT1: MOV AH,1 ;判断是否有按键按下
INT 16H
JZ WAIT1 ;无按键则跳回继续等待，有则退出

QUIT: CLI
MOV AX,0000H ;恢复IRQ10 原中断矢量
MOV ES,AX
MOV DI,IRQ10_IVADD
MOV AX,IP10_BAK ;恢复IRQ10 原中断处理程序入口偏移地址
MOV ES:[DI],AX
ADD DI,2
MOV AX,CS10_BAK ;恢复IRQ10 原中断处理程序入口段地址
MOV ES:[DI],AX
MOV DX,IRQ10_OCW1 ;恢复IRQ10 原中断屏蔽寄存器的屏蔽字
MOV AL,IM10_BAK
OUT DX,AL

MOV AX,0000H ;恢复IRQ7 原中断矢量
MOV ES,AX
MOV DI,IRQ7_IVADD
MOV AX,IP7_BAK ;恢复IRQ7 原中断处理程序入口偏移地址
MOV ES:[DI],AX
ADD DI,2
MOV AX,CS7_BAK ;恢复IRQ7 原中断处理程序入口段地址
MOV ES:[DI],AX
MOV DX,IRQ7_OCW1 ;恢复IRQ7 原中断屏蔽寄存器的屏蔽字
MOV AL,IM7_BAK
OUT DX,AL
STI

MOV AX,4C00H ;返回到DOS
INT 21H



MYISR10 PROC NEAR ;中断处理程序MYISR
PUSH AX
MOV AL,39H
MOV AH,0EH
INT 10H
MOV AL,20H
INT 10H
OVER10: MOV DX,IRQ10_OCW2 ;向PC 机内部8259 发送中断结束命令
MOV AL,20H
OUT DX,AL
MOV AL,20H
OUT 20H,AL
POP AX
IRET
MYISR10 ENDP

MYISR7 PROC NEAR ;中断处理程序MYISR
PUSH AX
MOV AL,39H
MOV AH,0EH
INT 10H
MOV AL,20H
INT 10H
OVER7: MOV DX,IRQ7_OCW2 ;向PC 机内部8259 发送中断结束命令
MOV AL,20H
OUT DX,AL
POP AX
IRET
MYISR7 ENDP

CODE ENDS
END START
